(* Denning Sacco Symmetric Key
#
# 1.  A	->	S:  	A, B
# 2.  S	->	A:  	{B, Kab, {Kab, A}Kbs}Kas
# 3.  A	->	B:  	{Kab,A}Kbs
# Strong secrecy of Kab
# 6.  B      ->     : {m}Kab versus {m}K with K fresh
*)


free a, b, c, s1, s2.

chan ca, cb, cs.

free ok.

fun senc/2.
fun sdec/2.
sdec(senc(x,y),y) -> x.

let processA(ch,ida,kas,idb) =
  out(ch,(ida,idb));
  in(ch,xa);
  let (=idb,xab,xmb) = sdec(xa,kas) in
  out(ch,xmb).

let processB(ch,idb,kbs,ida) =
  in(ch,yb);
  let (yab,=ida)= sdec(yb,kbs) in
  0.

let processS(ch,ida,kas,idb,kbs) =
	in(ch,zs);
	if zs = (ida,idb) then
	new kab;
	out(ch,senc((idb,kab,senc((kab,ida),kbs)),kas)).

let processSProp(ch,ida,kas,idb,kbs,s) =
	in(ch,zs);
	if zs = (ida,idb) then
	out(ch,senc((idb,s,senc((s,ida),kbs)),kas)).

// Main

let Scenario(s)=
  new kas; new kbs;
  (
    processA(ca,a,kas,b) | processB(cb,b,kbs,a) | processSProp(cs,a,kas,b,kbs,s)
  ).

let Preal = Scenario(s1).

let Pideal = Scenario(s2).

query trace_equiv(Preal,Pideal).
